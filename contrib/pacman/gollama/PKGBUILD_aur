pkgname='gollama'
pkgdesc='Go manage your Ollama models'
pkgver='1.27.6'
pkgrel='1'
arch=('x86_64')
url='https://github.com/sammcj/gollama'
license=('MIT')
depends=('glibc')
makedepends=('go')
optdepends=('ollama: ollama support.')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sammcj/gollama/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('2b2d77d52e321b86010739f0828b9de1f585c3ca86d3bc30e818d4d3e7b317676dd7fa526e349557eed7eae9630f8fc2ce50b4fbd524c329b4935ab00d6af24a')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -p build/
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS='-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw'
  go build -ldflags="-X main.Version=$pkgver" -o build .
}

check() {
  cd "$pkgname-$pkgver"
  go test ./...
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 build/$pkgname "$pkgdir"/usr/bin/$pkgname
}
